
buildscript {
    ext {
        springBootVersion = '2.6.2'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
    }
}

// 하위 프로젝트 공통 셋팅 (settings.gradle 파일 내 include 된 프로젝트)
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group 'org.example'
    version '1.0-SNAPSHOT'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
    }

    // 하위 모듈에서 공통으로 사용하는 세팅 추가
    dependencies {
        compileOnly 'org.projectlombok:lombok'

        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

    test {
        useJUnitPlatform()
    }
}

// core - business process module
project(':core') {
    bootJar { enabled = false } // bootJar 패키징 X
    jar { enabled = true }
}

// repository - db module
project(':repository') {
    bootJar { enabled = false } // bootJar 패키징 X
    jar { enabled = true }
}

// message - message module
project(':message') {
    bootJar { enabled = false } // bootJar 패키징 X
    jar { enabled = true }
}

// consumer - consumer module
project(':consumer') {
    bootJar { enabled = true }
    jar { enabled = true }

    dependencies {
        implementation project(':message')
    }
}

// 하위 프로젝트 의존성 관리 (internal-api)
project(':internal-api') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        implementation project(':core') // 컴파일 시 core 프로젝트 로드
        implementation project(':repository')
        implementation project(':message')
    }
}

// 하위 프로젝트 의존성 관리 (external-api)
project(':external-api') {
    bootJar { enabled = true }
    jar { enabled = false }

    dependencies {
        implementation project(':core') // 컴파일 시 core 프로젝트 로드
        implementation project(':repository')
        implementation project(':message')
    }
}